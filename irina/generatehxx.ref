* INTELIB_CONTRIBUTED
* Written by Igor Bronstein (<igor.bronstein_AT_intelib.org>)

$EXTERN AddSymbol;
$EXTERN TwoNewlines;

$ENTRY GenerateHxx {
  t.Directives =
	((<GetHxxName t.Directives>) (<GetHxxCont t.Directives>));
  e.Foo = <Prout 'Bug in "GenerateHxx" function. View field: ' e.Foo>;
}

GetHxxName {
  (t.Name (e.Foo (7 e.ModuleName) e.Bar1 (9) e.Bar2)) =
	e.ModuleName '.hxx';
  (t.Name (e.Foo (7 e.ModuleName) e.Bar1 (9 e.HxxFileName) e.Bar2)) =
	e.HxxFileName;
  e.Foo = <Prout 'Bug in "GetHxxName" function. View field: ' e.Foo>;
}

HxxPartsNumber {
  = 4;
}

GetHxxCont {
  t.Directives =
	<FillHxx 1 t.Directives <StartFilling 1 t.Directives>>;
  e.Foo = <Prout 'Bug in "GetHxxCont" function. View field: ' e.Foo>;
}

FillHxx {
  s.N t.Directives, <HxxPartsNumber> : s.N = ;
  s.N t.Directives e.Text, <HxxPartsNumber> : s.N =
	<TwoNewlines>
	e.Text;
  s.N t.Directives =
	<FillHxx <Add s.N 1> t.Directives <StartFilling <Add s.N 1> t.Directives>>;
  s.N t.Directives e.Text =
	<TwoNewlines>
	e.Text
	<FillHxx <Add s.N 1> t.Directives <StartFilling <Add s.N 1> t.Directives>>;
  e.Foo = <Prout 'Bug in "FillHxx" function. View field: ' e.Foo>;
}

StartFilling {
  1 t.Directives =
	<AddHeader>;
  2 (t.Name (e.Foo (1 e.PublicFunctions) e.Bar1 (5 e.NamesConvention) e.Bar2
  (6 e.CharsConvention) e.Bar3)) =
	<AddSymbol (e.PublicFunctions) (e.NamesConvention) (e.CharsConvention)>;
  3 (t.Name (e.Foo (2 e.PublicSymbols) e.Bar1 (5 e.NamesConvention) e.Bar2
  (6 e.CharsConvention) e.Bar3)) =
	<AddSymbol (e.PublicSymbols) (e.NamesConvention) (e.CharsConvention)>;
  4 (t.Name (e.Foo (11 e.InitFunction) e.Bar)) =
	<AddInitFunction e.InitFunction>;
  e.Foo = <Prout 'Bug in "StartFilling" function. View field: ' e.Foo>;
}

AddHeader {
  =
	'#ifndef INTELIB_REFAL_TRANSLATED_HXX\n'
	'#define INTELIB_REFAL_TRANSLATED_HXX\n'
	'\n'
	'#include "sexpress/sexpress.hpp"\n'
        '\n'
	'#include "refal/rclause.hpp"\n';
}

AddInitFunction {
  e.InitFunction = 
	'void ' e.InitFunction '();\n'
	'void ' e.InitFunction '(int argc, char **argv);\n'
	'void RefalFree();\n'
	'#endif\n';
}
