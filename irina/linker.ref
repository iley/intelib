* INTELIB_CONTRIBUTED
* Written by Igor Bronstein (<igor.bronstein_AT_intelib.org>)

$EXTERN GetDelimType;
$EXTERN GetVarType;
$EXTERN LastLine;
$EXTERN GetDelimCode;
$EXTERN GetEntryType;
$EXTERN GetExternType;
$EXTERN GetSymbolType;
$EXTERN MoreLinker;
$EXTERN CheckLinker;
$EXTERN GetDirectType;
$EXTERN NotEmptyPublics;


$ENTRY Linker {
  e.Foo () e.Bar = ;
  (t.FileName e.FileCont) e.Files =
	<CheckLinker
		<MoreLinker
			(ErrorsList '=' ())
			<NotEmptyPublics <GatherRest (t.FileName <GetDirectives <MergeInFiles e.Files (t.FileName e.FileCont)>>) (t.FileName e.FileCont) e.Files>>
		>
		(t.FileName e.FileCont) e.Files
	>;
  e.Foo = <Prout 'Bug in "Linker" function. View field: ' e.Foo>;
}

DirectivesTypesNumber {
  = 11;
}

ListsNumber {
  = 4;
}

$ENTRY NextNumbersFrom {
  = 100;
}

GetDirectives {
  e.FileCont = <GetSomethingMore () <GetSomething 1 () e.FileCont>>;
}

MergeInFiles {
  (t.FileName e.FileCont) e.Files =
	<Merge e.FileCont> <MergeInFiles e.Files>;
  = ;
  e.Foo = <Prout 'Bug in "MergeInFiles" function. View field: ' e.Foo>;
}

Merge {
  (s.LineNumber2 s.DirectType)
  ((s.LineNumber3 s.DelimType s.LCBracket) e.Foo (s.LineNumber s.DelimType Semicolon) (s.LineNumber4 s.DelimType s.RCBracket)) e.Text,
	<GetDirectType> : s.DirectType,
	<GetDelimCode '{'> : s.LCBracket,
	<GetDelimCode '}'> : s.RCBracket =
	e.Foo (s.LineNumber s.DelimType Semicolon) <Merge e.Text>;
  (s.LineNumber2 s.DirectType)
  ((s.LineNumber3 s.DelimType s.LCBracket) e.Foo (s.LineNumber4 s.DelimType s.RCBracket)) e.Text,
	<GetDirectType> : s.DirectType,
	<GetDelimCode '{'> : s.LCBracket,
	<GetDelimCode '}'> : s.RCBracket =
	e.Foo (<LastLine e.Foo> <GetDelimType> Semicolon) <Merge e.Text>;
  = ;

  t.Foo e.Text =
	<Merge e.Text>;

  e.Foo = <Prout 'Bug in "Merge" function. View field: ' e.Foo>;
}

GetSomething {
  1 (e.Already)
  (s.LineNumber1 s.SymbolType 'DECLARE_PUBLIC_FUNCTION') (s.LineNumber2 s.DelimType Equal)
  (s.LineNumber3 s.SymbolType e.FunName) (s.LineNumber4 s.DelimType Semicolon) e.Text =
	<GetSomething 1 (e.Already (s.LineNumber3 e.FunName)) e.Text>;
  2 (e.Already)
  (s.LineNumber1 s.SymbolType 'DECLARE_PUBLIC_SYMBOL') (s.LineNumber2 s.DelimType Equal)
  (s.LineNumber3 s.SymbolType e.SymbName) (s.LineNumber4 s.DelimType Semicolon) e.Text =
	<GetSomething 2 (e.Already (s.LineNumber3 e.SymbName)) e.Text>;
  3 (e.Already)
  (s.LineNumber1 s.SymbolType 'DECLARE_EXTERNAL_FUNCTION') (s.LineNumber2 s.DelimType Equal)
  (s.LineNumber3 s.SymbolType e.FunName) (s.LineNumber4 s.DelimType Semicolon) e.Text =
	<GetSomething 3 (e.Already (s.LineNumber3 e.FunName)) e.Text>;
  4 (e.Already)
  (s.LineNumber1 s.SymbolType 'DECLARE_EXTERNAL_SYMBOL') (s.LineNumber2 s.DelimType Equal)
  (s.LineNumber3 s.SymbolType e.SymbName) (s.LineNumber4 s.DelimType Semicolon) e.Text =
	<GetSomething 4 (e.Already (s.LineNumber3 e.SymbName)) e.Text>;
  5 (e.Already)
  (s.LineNumber1 s.SymbolType 'DECLARE_NAME') (s.LineNumber2 s.SymbolType e.Name)
  (s.LineNumber3 s.DelimType Equal)
  (s.LineNumber4 s.SymbolType e.Ident) (s.LineNumber5 s.DelimType Semicolon) e.Text =
	<GetSomething 5 (e.Already (s.LineNumber4 (e.Name) (e.Ident))) e.Text>;
  5 (e.Already)
  (s.LineNumber1 s.SymbolType 'DECLARE_NAME') (s.LineNumber2 s.VarType e.Name)
  (s.LineNumber3 s.DelimType Equal)
  (s.LineNumber4 s.SymbolType e.Ident) (s.LineNumber5 s.DelimType Semicolon) e.Text =
	<GetSomething 5 (e.Already (s.LineNumber4 (e.Name) (e.Ident))) e.Text>;
  6 (e.Already)
  (s.LineNumber1 s.SymbolType 'DECLARE_CHAR') (s.LineNumber2 s.TextConstantType s.Char)
  (s.LineNumber3 s.DelimType Equal)
  (s.LineNumber4 s.SymbolType e.Ident) (s.LineNumber5 s.DelimType Semicolon) e.Text =
	<GetSomething 6 (e.Already (s.LineNumber4 (s.Char) (e.Ident))) e.Text>;
  7 (e.Already)
  (s.LineNumber1 s.SymbolType 'MODULE_NAME') (s.LineNumber2 s.DelimType Equal)
  (s.LineNumber3 s.TextConstantType e.ModuleName) (s.LineNumber4 s.DelimType Semicolon) e.Text =
	<GetSomething 7 (e.ModuleName) e.Text>;
  8 (e.Already)
  (s.LineNumber1 s.SymbolType 'CXX_FILE_PATH') (s.LineNumber2 s.DelimType Equal)
  (s.LineNumber3 s.TextConstantType e.FilePath) (s.LineNumber4 s.DelimType Semicolon) e.Text =
	<GetSomething 8 (e.FilePath) e.Text>;
  9 (e.Already)
  (s.LineNumber1 s.SymbolType 'HXX_FILE_PATH') (s.LineNumber2 s.DelimType Equal)
  (s.LineNumber3 s.TextConstantType e.FilePath) (s.LineNumber4 s.DelimType Semicolon) e.Text =
	<GetSomething 9 (e.FilePath) e.Text>;
  10 (e.Already)
  (s.LineNumber1 s.SymbolType 'DECLARE_USED_MODULE') (s.LineNumber2 s.DelimType Equal)
  (s.LineNumber3 s.TextConstantType e.UsedModule) (s.LineNumber4 s.DelimType Semicolon) e.Text =
	<GetSomething 10 (e.Already (s.LineNumber3 e.UsedModule)) e.Text>;
  11 (e.Already)
  (s.LineNumber1 s.SymbolType 'INIT_FUNCTION_NAME') (s.LineNumber2 s.DelimType Equal)
  (s.LineNumber3 s.SymbolType e.FunName) (s.LineNumber4 s.DelimType Semicolon) e.Text =
	<GetSomething 11 (e.FunName) e.Text>;
  s.N (e.Already) t.Foo e.Text =
	t.Foo <GetSomething s.N (e.Already) e.Text>;
  s.N (e.Already) = s.N (e.Already);
  e.Foo = <Prout 'Bug in "GetSomething" function. View field: ' e.Foo>;
}

GetSomethingMore {
  (e.Got) e.Text s.N (e.Already), <DirectivesTypesNumber> : s.N = (e.Got (s.N e.Already));
  (e.Got) e.Text 6 (e.Already) =
	<GetSomethingMore (e.Got (6 e.Already)) <GetSomething 7 ('refalpart') e.Text>>;
  (e.Got) e.Text 10 (e.Already) =
	<GetSomethingMore (e.Got (10 e.Already)) <GetSomething 11 ('RefalInit') e.Text>>;
  (e.Got) e.Text s.N (e.Already) =
	<GetSomethingMore (e.Got (s.N e.Already)) <GetSomething <Add s.N 1> () e.Text>>;
  e.Foo = <Prout 'Bug in "GetSomethingMore" function. View field: ' e.Foo>;
}

GatherRest {
  (t.FileName t.Got) e.Files = <GatherMore (t.FileName t.Got) <Gather 1 e.Files>>;
  e.Foo = <Prout 'Bug in "GatherRest" function. View field: ' e.Foo>;
}

Gather {
  s.N e.Files = e.Files <GatherInFiles s.N () e.Files> s.N;
  e.Foo = <Prout 'Bug in "Gather" function. View field: ' e.Foo>;
}

GatherMore {
  (t.FileName (e.Got)) e.Files (e.Already) s.N, <ListsNumber> : s.N =
	(t.FileName (e.Got (<Add <NextNumbersFrom> s.N> e.Already)));
  (t.FileName (e.Got)) e.Files (e.Already) s.N =
	<GatherMore (t.FileName (e.Got (<Add <NextNumbersFrom> s.N> e.Already))) <Gather <Add s.N 1> e.Files>>;
  e.Foo = <Prout 'Bug in "GatherMore" function. View field: ' e.Foo>;
}

GatherInFiles {
  s.N (e.Already) t.File e.Files = 
	<GatherInFiles s.N <GatherInOneFile s.N (e.Already) t.File> e.Files>;
  s.N (e.Already) = (e.Already);
  e.Foo = <Prout 'Bug in "GatherInFiles" function. View field: ' e.Foo>;
}

GatherInOneFile {
  1 (e.Already) (t.FileName e.FileCont) =
	<GatherVars (e.Already) e.FileCont>;
  2 (e.Already) (t.FileName e.FileCont) =
	<GatherSymbols (e.Already) e.FileCont>;
  3 (e.Already) (t.FileName e.FileCont) =
	<GatherEntries (e.Already) e.FileCont>;
  4 (e.Already) (t.FileName e.FileCont) =
	<GatherExterns (e.Already) e.FileCont>;
  e.Foo = <Prout 'Bug in "GatherInOneFile" function. View field: ' e.Foo>;
}

GatherVars {
  (e.Already) (s.LineNumber s.VarType e.Var) e.Text,
	<GetVarType> : s.VarType =
	<GatherVars <AddVarOrSymbol (e.Already) (e.Var)> e.Text>;
  (e.Already)((s.LineNumber1 s.DelimType s.LBracket) e.Foo (s.LineNumber2 s.DelimType s.RBracket)) e.Text,
	<GetDelimType> : s.DelimType =
	<GatherVars <GatherVars (e.Already) e.Foo> e.Text>;
  (e.Already)(s.LineNumber s.Type e.Foo) e.Text =
	<GatherVars (e.Already) e.Text>;
  (e.Already) = (e.Already);
  e.Foo = <Prout 'Bug in "GatherVars" function. View field: ' e.Foo>;
}

GatherSymbols {
  (e.Already) (s.LineNumber0 s.ExternType) (s.LineNumber1 s.SymbolType e.Symbol)
  (s.LineNumber2 s.DelimType Semicolon) e.Text,
	<GetExternType> : s.ExternType,
	<GetSymbolType> : s.SymbolType =
	<GatherSymbols (e.Already) e.Text>;
  (e.Already) (s.LineNumber0 s.EntryType) (s.LineNumber1 s.SymbolType e.Symbol)
  ((s.LineNumber2 s.DelimType s.LCBracket) e.FunCont (s.LineNumber3 s.DelimType s.RCBracket)) e.Text,
	<GetSymbolType> : s.SymbolType,
	<GetEntryType> : s.EntryType,
	<GetDelimCode '{'> : s.LCBracket,
	<GetDelimCode '}'> : s.RCBracket =
	<GatherSymbols <GatherSymbols (e.Already) e.FunCont> e.Text>;

  (e.Already) (s.LineNumber0 s.DirectivesType)
  ((s.LineNumber1 s.DelimType s.LCBracket) e.Foo (s.LineNumber2 s.DelimType s.RCBracket))
  e.Text,
	<GetDirectType> : s.DirectivesType,
	<GetDelimCode '{'> : s.LCBracket,
	<GetDelimCode '}'> : s.RCBracket =
	<GatherSymbols (e.Already) e.Text>;

  (e.Already) (s.LineNumber1 s.SymbolType e.Symbol)
  ((s.LineNumber2 s.DelimType s.LCBracket) e.FunCont (s.LineNumber3 s.DelimType s.RCBracket)) e.Text,
	<GetSymbolType> : s.SymbolType,
	<GetDelimCode '{'> : s.LCBracket,
	<GetDelimCode '}'> : s.RCBracket =
	<GatherSymbols <GatherSymbols (e.Already) e.FunCont> e.Text>;
  (e.Already)
  ((s.LineNumber1 s.DelimType s.LFBracket) (s.LineNumber2 s.SymbolType e.Symbol)
  e.FunArgs (s.LineNumber3 s.DelimType s.RFBracket)) e.Text,
	<GetSymbolType> : s.SymbolType,
	<GetDelimCode '<'> : s.LFBracket,
	<GetDelimCode '>'> : s.RFBracket =
	<GatherSymbols <GatherSymbols (e.Already) e.FunArgs> e.Text>;
  (e.Already) (s.LineNumber s.SymbolType e.Symbol) e.Text,
	<GetSymbolType> : s.SymbolType =
	<GatherSymbols <AddVarOrSymbol (e.Already) (e.Symbol)> e.Text>;
  (e.Already)
  ((s.LineNumber1 s.DelimType s.LBracket) e.Foo (s.LineNumber3 s.DelimType s.RBracket)) e.Text,
	<GetSymbolType> : s.SymbolType,
	<GetDelimType> : s.DelimType =
	<GatherSymbols <GatherSymbols (e.Already) e.Foo> e.Text>;
  (e.Already) (s.LineNumber s.Type e.Foo) e.Text =
	<GatherSymbols (e.Already) e.Text>;
  (e.Already) = (e.Already);
  e.Foo = <Prout 'Bug in "GatherSymbols" function. View field: ' e.Foo>;
}

GatherEntries {
  (e.Already) (s.LineNumber0 s.ExternType) (s.LineNumber1 s.SymbolType e.Symbol)
  (s.LineNumber2 s.DelimType Semicolon) e.Text,
	<GetExternType> : s.ExternType,
	<GetSymbolType> : s.SymbolType =
	<GatherEntries (e.Already) e.Text>;
  (e.Already) (s.LineNumber0 s.EntryType) (s.LineNumber1 s.SymbolType e.Symbol)
  ((s.LineNumber2 s.DelimType s.LCBracket) e.FunCont (s.LineNumber3 s.DelimType s.RCBracket)) e.Text,
	<GetSymbolType> : s.SymbolType,
	<GetEntryType> : s.EntryType,
	<GetDelimCode '{'> : s.LCBracket,
	<GetDelimCode '}'> : s.RCBracket =
	<GatherEntries <AddEntry (e.Already) (e.Symbol)> e.Text>;

  (e.Already) (s.LineNumber0 s.DirectivesType)
  ((s.LineNumber1 s.DelimType s.LCBracket) e.Foo (s.LineNumber2 s.DelimType s.RCBracket))
  e.Text,
	<GetDirectType> : s.DirectivesType,
	<GetDelimCode '{'> : s.LCBracket,
	<GetDelimCode '}'> : s.RCBracket =
	<GatherEntries (e.Already) e.Text>;

  (e.Already) (s.LineNumber1 s.SymbolType e.Symbol)
  ((s.LineNumber2 s.DelimType s.LCBracket) e.FunCont (s.LineNumber3 s.DelimType s.RCBracket)) e.Text,
	<GetSymbolType> : s.SymbolType,
	<GetDelimCode '{'> : s.LCBracket,
	<GetDelimCode '}'> : s.RCBracket =
	<GatherEntries (e.Already) e.Text>;
  (e.Already) = (e.Already);
  e.Foo = <Prout 'Bug in "GatherEntries" function. View field: ' e.Foo>;
}

GatherExterns {
  (e.Already) (s.LineNumber0 s.ExternType) (s.LineNumber1 s.SymbolType e.Symbol)
  (s.LineNumber2 s.DelimType Semicolon) e.Text,
	<GetExternType> : s.ExternType,
	<GetSymbolType> : s.SymbolType =
	<GatherExterns <AddVarOrSymbol (e.Already) (e.Symbol)> e.Text>;
  (e.Already) (s.LineNumber0 s.EntryType) (s.LineNumber1 s.SymbolType e.Symbol)
  ((s.LineNumber2 s.DelimType s.LCBracket) e.FunCont (s.LineNumber3 s.DelimType s.RCBracket)) e.Text,
	<GetSymbolType> : s.SymbolType,
	<GetEntryType> : s.EntryType,
	<GetDelimCode '{'> : s.LCBracket,
	<GetDelimCode '}'> : s.RCBracket =
	<GatherExterns (e.Already) e.Text>;

  (e.Already) (s.LineNumber0 s.DirectivesType)
  ((s.LineNumber1 s.DelimType s.LCBracket) e.Foo (s.LineNumber2 s.DelimType s.RCBracket))
  e.Text,
	<GetDirectType> : s.DirectivesType,
	<GetDelimCode '{'> : s.LCBracket,
	<GetDelimCode '}'> : s.RCBracket =
	<GatherExterns (e.Already) e.Text>;

  (e.Already) (s.LineNumber1 s.SymbolType e.Symbol)
  ((s.LineNumber2 s.DelimType s.LCBracket) e.FunCont (s.LineNumber3 s.DelimType s.RCBracket)) e.Text,
	<GetSymbolType> : s.SymbolType,
	<GetDelimCode '{'> : s.LCBracket,
	<GetDelimCode '}'> : s.RCBracket =
	<GatherExterns (e.Already) e.Text>;
  (e.Already) = (e.Already);
  e.Foo = <Prout 'Bug in "GatherExterns" function. View field: ' e.Foo>;
}

AddVarOrSymbol {
  (e.Foo (e.Var) e.Bar) (e.Var) = (e.Foo (e.Var) e.Bar);
  (e.Foo) (e.Var) = (e.Foo (e.Var));
  e.Foo = <Prout 'Bug in "AddVarOrSymbol" function. View field: ' e.Foo>;
}

AddEntry {
  (e.Foo (s.Number e.Fun) e.Bar) (e.Fun) = (e.Foo (<Add s.Number 1> e.Fun) e.Bar);
  (e.Foo) (e.Fun) = (e.Foo (0 e.Fun));
  e.Foo = <Prout 'Bug in "AddEntry" function. View field: ' e.Foo>;
}
